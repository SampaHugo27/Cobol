	IDENTIFICATION                      DIVISION.                   
       *----------------------------------------------------------------
        PROGRAM-ID.     EXRELATO.                                       
        AUTHOR.         HUGO SAMPAIO.                                   
        INSTALLATION.   QINTESS.                                        
        DATE-WRITTEN.                                                   
        DATE-COMPILED.                                                  
                                                                        
       *REMARKS.        O PROGRAMA LE OS REGISTROS DO ARQUIVO DE ENTRADA
       *                ARQENT, EXIBINDO O TOTAL DE GASTOS DE UM DADO   
       *                CLIENTE USANDO COMO CHAVE DE QUEBRA O CAMPO CPF 
       *                E CAMPO VALOR GASTO PARA SUMARIZAR. LOGO EM     
       *                SEGUIDA, SAO GRAVADOS PARA CADA REGISTRO DE     
       *                CLIENTE SEUS DADOS QUE SAO CPF, NOME-CLIENTE,   
       *                E VALOR-TOTAL SUMARIZADO POR CLIENTE NO ARQUIVO 
       *                DE SAIDA ARQSAI E DEPOIS ESSES MESMOS DADOS SAO 
       *                POSTERIORMENTE GRAVADOS EM UM RELATORIO         
       *----------------------------------------------------------------
	ENVIRONMENT                         DIVISION.                   
       *----------------------------------------------------------------
        CONFIGURATION                       SECTION.                    
       *----------------------------------------------------------------
        SOURCE-COMPUTER.                    IBM-PC.                     
        OBJECT-COMPUTER.                    IBM-PC.                     
        SPECIAL-NAMES.                      DECIMAL-POINT IS COMMA.     
       *----------------------------------------------------------------
        INPUT-OUTPUT                        SECTION.                    
       *----------------------------------------------------------------
        FILE-CONTROL.                                                   
            SELECT ARQENT  ASSIGN TO ARQENT                             
            ORGANIZATION   IS  SEQUENTIAL                               
            ACCESS MODE    IS  SEQUENTIAL                               
            FILE STATUS    IS  WS-FSARQENT.                             
       *----------------------------------------------------------------
            SELECT ARQSAI  ASSIGN TO ARQSAI                             
            ORGANIZATION   IS  SEQUENTIAL 
	    ACCESS MODE    IS  SEQUENTIAL                               
            FILE STATUS    IS  WS-FSARQSAI.                             
       *----------------------------------------------------------------
            SELECT RELATO  ASSIGN TO RELATO                             
            FILE STATUS    IS  WS-FSRELATO.                             
       *----------------------------------------------------------------
        DATA                                DIVISION.                   
       *----------------------------------------------------------------
        FILE                                SECTION.                    
       *----------------------------------------------------------------
        FD  ARQENT                                                      
            LABEL RECORD   ARE STANDARD                                 
            RECORD         CONTAINS 70 CHARACTERS                       
            DATA RECORD    IS REG-ENT                                   
            RECORDING MODE IS F.                                        
                                                                        
        01  REG-ENT.                                                    
            05  CPF-ENT                     PIC 9(11).
		          05  NOME-ENT                    PIC X(30).                   
            05  VALOR-GASTO                 PIC 9(15)V99.                
            05  DATA-COMPRA-ENT             PIC 9(08).                   
            05  FILLER                      PIC X(04).                   
       *---------------------------------------------------------------- 
        FD  ARQSAI                                                       
            LABEL RECORD   ARE STANDARD                                  
            RECORD         CONTAINS 70 CHARACTERS                        
            DATA RECORD    IS REG-SAI                                    
            RECORDING MODE IS F.                                         
                                                                         
        01  REG-SAI.                                                     
            05  CPF-SAI                     PIC 9(11).                   
            05  NOME-SAI                    PIC X(30).                   
            05  VALOR-TOTAL-SAI             PIC 9(15)V99.                
            05  DATA-COMPRA-SAI             PIC 9(08).                   
            05  FILLER                      PIC X(04).                   
       *----------------------------------------------------------------*
	FD  RELATO                                                       
            LABEL RECORD   IS OMITTED.                                   
                                                                         
        01  REG-ATR                         PIC X(80).                   
       *----------------------------------------------------------------*
        WORKING-STORAGE                     SECTION.                     
       *----------------------------------------------------------------*
       *                   DECLARACAO DE VARIAVEIS                      *
       *----------------------------------------------------------------*
       *                   VARIAVEIS DE FILE-STATUS                     *
       *----------------------------------------------------------------*
        77  WS-FSARQENT                     PIC X(02)     VALUE '00'.    
        77  WS-FSARQSAI                     PIC X(02)     VALUE '00'.    
        77  WS-FSRELATO                     PIC X(02)     VALUE '00'.    
       *----------------------------------------------------------------*
        77  WS-TOTAL-GASTO                  PIC 9(15)V99  VALUE ZEROS.   
        77  WS-TOTAL-RELAT                  PIC 9(15)V99  VALUE ZEROS.   
        77  WS-TOTAL-CLIENTE                PIC 9(15)V99  VALUE ZEROS. 
	77  DATA-C                          PIC 9(08)     VALUE ZEROS.   
        77  CPF-11                          PIC 9(11)     VALUE ZEROS.   
        77  WS-LIMITE                       PIC 9(04)     VALUE ZEROS.   
       *----------------------------------------------------------------*
       *                 INDICES DA TABELA BIDIMENSIONAL                *
       *----------------------------------------------------------------*
        77  WS-IND                          PIC 9(04)     VALUE ZEROS.   
        77  WS-IND2                         PIC 9(04)     VALUE ZEROS.   
       *----------------------------------------------------------------*
       *                 VARIAVEIS DE CHAVES DE QUEBRA                  *
       *----------------------------------------------------------------*
        77  WS-CHAVE-QUEBRA-ANT             PIC 9(11)     VALUE ZEROS.   
        77  WS-CHAVE-QUEBRA-ATU             PIC 9(11)     VALUE ZEROS.   
       *----------------------------------------------------------------*
       *                         CONTADORES                             *
       *----------------------------------------------------------------*
        77  WS-CT-LIN                       PIC 99        VALUE 50.      
        77  WS-CT-PAG                       PIC 999       VALUE ZEROS.
	77  WS-QTDE-REGSAI                  PIC 9(03)     VALUE ZEROS.   
        77  WS-QTDE-REG-LIDOS               PIC 9(03)     VALUE ZEROS.   
       *----------------------------------------------------------------*
       *              FLAGS A SEREM UTILIZADAS NO PROGRAMA              *
       *----------------------------------------------------------------*
        77  FLAG1                           PIC 9         VALUE ZEROS.   
        77  FLAG2                           PIC 9         VALUE ZEROS.   
       *----------------------------------------------------------------*
       *              AREA DE OCCURS - CRIACAO DE TABELA                *
       *----------------------------------------------------------------*
        01 WS-TABELA-CPF.                                                
           03 WS-TB-CPF    OCCURS 40 TIMES.                              
              05 WS-TAB-CPF                 PIC 9(11)     VALUE ZEROS.   
              05 WS-TB-CPF-DV  OCCURS 20 TIMES.                          
                 07 WS-TAB-DATA             PIC 9(08)     VALUE ZEROS.   
                 07 WS-TAB-VALOR            PIC 9(15)V99  VALUE ZEROS.   
       *----------------------------------------------------------------*
       *                       AREA DE ABEND                            *
       *----------------------------------------------------------------*
        77  WS-ABENDA                       PIC X(08)   VALUE 'ABENDA31'.
        01  WS-AREA.                                                     
            05 WS-AREA-PGM                  PIC X(08)   VALUE 'VRB643S'. 
            05 WS-AREA-STAT                 PIC X(03)   VALUE SPACES.    
            05 WS-AREA-MSG                  PIC X(50)   VALUE SPACES.    
                                                                         
        01  WS-CODIGO-AREA                  PIC X(30)   VALUE SPACES.    
       *----------------------------------------------------------------*
       *         LAYOUT DO RELATORIO - AREA DE CABEÇALHOS               *
       *----------------------------------------------------------------*
        01  CAB-01.                                                      
            05 FILLER       PIC X(29)       VALUE SPACES.                
            05 FILLER       PIC X(16)       VALUE 'BRADESCO CARTOES'.    
            05 FILLER       PIC X(19)       VALUE SPACES.                
            05 FILLER       PIC X(06)       VALUE 'DATA: '.              
            05 DIA          PIC 99.                                      
            05 FILLER       PIC X(01)       VALUE '/'.
		          05 MES          PIC 99.                                      
            05 FILLER       PIC X(01)       VALUE '/'.                   
            05 ANO          PIC 9999.                                    
       *----------------------------------------------------------------*
        01  CAB-02.                                                      
            05 FILLER       PIC X(01)  VALUE SPACES.                     
            05 FILLER       PIC X(08)  VALUE 'PVRB643S'.                 
            05 FILLER       PIC X(12)  VALUE SPACES.                     
            05 FILLER       PIC X(25)  VALUE 'RELATORIO TOTALIZADOR DE '.
            05 FILLER       PIC X(06)  VALUE 'GASTOS'.                   
            05 FILLER       PIC X(17)  VALUE SPACES.                     
            05 FILLER       PIC X(08)  VALUE 'PAGINA: '.                 
            05 PAGINA       PIC 999.                                     
       *----------------------------------------------------------------*
        01  CAB-03.                                                      
            05 FILLER       PIC X(80)  VALUE ALL '-'.                    
       *----------------------------------------------------------------*
        01  CAB-04. 
	    05 FILLER       PIC X(01)  VALUE SPACES.                     
            05 FILLER       PIC X(03)  VALUE 'CPF'.                      
            05 FILLER       PIC X(15)  VALUE SPACES.                     
            05 FILLER       PIC X(04)  VALUE 'NOME'.                     
            05 FILLER       PIC X(41)  VALUE SPACES.                     
            05 FILLER       PIC X(11)  VALUE 'VALOR TOTAL'.              
            05 FILLER       PIC X(05)  VALUE SPACES.                     
       *----------------------------------------------------------------*
       *         LAYOUT DO RELATORIO - AREA DE DETALHE                  *
       *----------------------------------------------------------------*
        01  DETALHE.                                                     
            05 FILLER       PIC X(01)  VALUE SPACES.                     
            05 CPF-NOVE     PIC 9(09).                                   
            05 FILLER       PIC X(01)  VALUE '-'.                        
            05 CPF-DOIS     PIC 9(02).                                   
            05 FILLER       PIC X(06)  VALUE SPACES.                     
            05 NOME         PIC X(30).                                   
            05 FILLER       PIC X(04).
	    05 VALOR-TOT-C  PIC ZZZ.ZZZ.ZZZ.ZZZ.ZZ9,99  VALUE ZEROS.     
            05 FILLER       PIC X(05)  VALUE SPACES.                     
       *----------------------------------------------------------------*
        01  DETALHE-VAZIO.                                               
            05 FILLER       PIC X(01)  VALUE SPACES.                     
            05 FILLER       PIC X(24)  VALUE 'ARQUIVO DE ENTRADA VAZIO'. 
            05 FILLER       PIC X(55)  VALUE SPACES.                     
       *----------------------------------------------------------------*
       *         LAYOUT DO RELATORIO - AREA DE TOTALIZADORES            *
       *----------------------------------------------------------------*
        01  TOT-01.                                                      
            05 FILLER       PIC X(01) VALUE SPACES.                      
            05 FILLER       PIC X(26) VALUE 'VALOR TOTAL DIVIDA........'.
            05 FILLER       PIC X(22) VALUE '.....................:'     
            05 VALOR-TOT-D  PIC ZZZ.ZZZ.ZZZ.ZZZ.ZZ9,99  VALUE ZEROS.     
            05 FILLER       PIC X(05) VALUE SPACES.                      
       *----------------------------------------------------------------*
        01  TOT-02. 
	    05 FILLER       PIC X(01) VALUE SPACES.                      
            05 FILLER       PIC X(26) VALUE 'TOTAL REGISTROS IMPRESSOS:'.
            05 FILLER       PIC X(10) VALUE SPACES.                      
            05 TOT-REG      PIC Z.ZZZ.ZZZ.ZZ9  VALUE ZEROS.              
            05 FILLER       PIC X(30).                                   
       *----------------------------------------------------------------*
       *----------------------------------------------------------------*
        PROCEDURE                           DIVISION.                    
       *---------------------------------------------------------------* 
        0000-PRINCIPAL                      SECTION.                     
       *---------------------------------------------------------------- 
            PERFORM 1000-INICIO        THRU  1000-99-INICIO-EXIT.        
            PERFORM 2000-PROCESSA      THRU  2000-99-PROCESSA-EXIT       
                                       UNTIL WS-FSARQENT EQUAL '10'.     
            PERFORM 3000-FINALIZA      THRU  3000-99-FINALIZA-EXIT.      
            STOP RUN.                                                    
       *---------------------------------------------------------------- 
        0000-99-PRINCIPAL-EXIT.             EXIT.
       *---------------------------------------------------------------- 
       *---------------------------------------------------------------- 
        1000-INICIO                         SECTION.                     
       *---------------------------------------------------------------- 
            DISPLAY '**************************************************'.
            DISPLAY '           INICIANDO O PROGRAMA...                '.
            DISPLAY '**************************************************'.
            DISPLAY '                                                  '.
       *---------------------------------------------------------------- 
       * ABERTURA DE ARQUIVOS E VERIFICACAO DE FILE STATUS               
                                                                         
            MOVE '1000-INICIO'         TO         WS-CODIGO-AREA.        
                                                                         
            OPEN INPUT   ARQENT.                                         
            IF WS-FSARQENT             NOT EQUAL '00'                    
               DISPLAY 'ERRO NO: '                WS-CODIGO-AREA         
               DISPLAY 'STATUS:  '                WS-FSARQENT            
               MOVE WS-FSARQENT        TO         WS-AREA-STAT
	       MOVE 'ERRO NA ABERTURA DO ARQUIVO DE ENTRADA ARQENT'     
                                       TO         WS-AREA-MSG           
               CALL WS-ABENDA          USING      WS-AREA               
            END-IF.                                                     
       *----------------------------------------------------------------
            OPEN OUTPUT  ARQSAI.                                        
            IF WS-FSARQSAI             NOT EQUAL '00'                   
               DISPLAY 'ERRO NO: '                WS-CODIGO-AREA        
               DISPLAY 'STATUS:  '                WS-FSARQSAI           
               MOVE WS-FSARQSAI        TO         WS-AREA-STAT          
               MOVE 'ERRO NA ABERTURA DO ARQUIVO DE SAIDA ARQSAI'       
                                       TO         WS-AREA-MSG           
               CALL WS-ABENDA          USING      WS-AREA               
            END-IF.                                                     
       *----------------------------------------------------------------
            OPEN OUTPUT  RELATO.                                        
            IF WS-FSRELATO             NOT EQUAL  '00'                  
               DISPLAY 'ERRO NO: '                WS-CODIGO-AREA
	       DISPLAY 'STATUS:  '                WS-FSRELATO            
               MOVE   WS-FSRELATO      TO         WS-AREA-STAT           
               MOVE 'ERRO NA ABERTURA DO ARQUIVO DE SAIDA ARQSAI'        
                                       TO         WS-AREA-MSG            
               CALL WS-ABENDA          USING      WS-AREA                
            END-IF.                                                      
       *---------------------------------------------------------------- 
            PERFORM 2220-LEITURA.                                        
                                                                         
       * VERIFICA SE O ARQUIVO DE ENTRADA ESTA VAZIO                     
                                                                         
            IF WS-QTDE-REG-LIDOS            GREATER 0                    
               MOVE WS-CHAVE-QUEBRA-ATU     TO    WS-CHAVE-QUEBRA-ANT    
            ELSE                                                         
               DISPLAY '************************************************'
               DISPLAY '********** ARQUIVO DE ENTRADA VAZIO ************'
               DISPLAY '************************************************'
               MOVE  ZEROS                  TO    WS-CT-LIN
		             PERFORM 3330-IMPRIMA-ARQ-VAZIO                           
            END-IF.                                                     
       *----------------------------------------------------------------
        1000-99-INICIO-EXIT.                EXIT.                       
       *----------------------------------------------------------------
       *----------------------------------------------------------------
        2000-PROCESSA                       SECTION.                    
       *----------------------------------------------------------------
       * VERIFICACAO ENTRE CHAVES ATUAL E ANTERIOR                      
                                                                        
            IF WS-CHAVE-QUEBRA-ATU   NOT EQUAL    WS-CHAVE-QUEBRA-ANT   
               PERFORM 2230-GRAVACAO                                    
               MOVE  ZEROS                  TO    WS-TOTAL-CLIENTE      
               MOVE WS-CHAVE-QUEBRA-ATU     TO    WS-CHAVE-QUEBRA-ANT   
               ADD  WS-TOTAL-GASTO          TO    WS-TOTAL-RELAT        
               MOVE  ZEROS                  TO    WS-TOTAL-GASTO        
            END-IF.                                                     

           * MOVIMENTACAO PARA O ARQUIVO ARQSAI                       
                                                                  
            MOVE CPF-ENT                    TO    CPF-SAI.        
            MOVE NOME-ENT                   TO    NOME-SAI.       
                                                                  
            ADD  VALOR-GASTO                TO    WS-TOTAL-GASTO. 
            MOVE WS-TOTAL-GASTO             TO    VALOR-TOTAL-SAI.
            MOVE DATA-COMPRA-ENT            TO    DATA-COMPRA-SAI.
                                                                  
       * MOVIMENTACAO PARA O ARQUIVO RELATO                       
                                                                  
            IF   WS-CT-LIN GREATER 49                             
                 IF PAGINA GREATER 0                              
                    MOVE  SPACES            TO    REG-ATR         
                    WRITE REG-ATR                                 
                 END-IF                                           
                                                                  
                 MOVE ZEROS TO WS-CT-LIN 
		 PERFORM 2240-CABECALHO-PARTE1                          
                 PERFORM 2250-CABECALHO-PARTE2                          
            END-IF.                                                     
                                                                        
            PERFORM 2255-CARREGA-TABNOVA1.                              
            PERFORM 2260-IMPRIMA-DET.                                   
            PERFORM 2220-LEITURA.                                       
       *----------------------------------------------------------------
        2000-99-PROCESSA-EXIT.              EXIT.                       
       *----------------------------------------------------------------
       *----------------------------------------------------------------
        2220-LEITURA                        SECTION.                    
       *----------------------------------------------------------------
            MOVE '2220-LEITURA'             TO    WS-CODIGO-AREA.       
                                                                        
            READ ARQENT.                                                
                                                                        
            IF  WS-FSARQENT  NOT EQUAL '00'
	    AND WS-FSARQENT  NOT EQUAL '10'                             
               DISPLAY 'ERRO NO: '                WS-CODIGO-AREA        
               DISPLAY 'STATUS:  '                WS-FSARQENT           
               MOVE WS-FSARQENT             TO    WS-AREA-STAT          
               MOVE 'ERRO AO LER ARQUIVO ENTRADA'                       
                                            TO    WS-AREA-MSG           
               CALL WS-ABENDA               USING WS-AREA               
                                                                        
            ELSE                                                        
               IF   WS-FSARQENT = '10'                                  
                    GO TO  2220-99-LEITURA-EXIT                         
               END-IF                                                   
            END-IF.                                                     
                                                                        
            MOVE CPF-ENT                    TO    WS-CHAVE-QUEBRA-ATU.  
            ADD  1                          TO    WS-QTDE-REG-LIDOS.    
       *----------------------------------------------------------------
        2220-99-LEITURA-EXIT.               EXIT.
       *----------------------------------------------------------------
       *----------------------------------------------------------------
        2230-GRAVACAO                       SECTION.                    
       *----------------------------------------------------------------
            MOVE '2230-GRAVACAO'            TO    WS-CODIGO-AREA.       
            WRITE REG-SAI.                                              
                                                                        
       * VERIFICACAO DE FILE STATUS PARA GRAVACAO EM ARQSAI'            
                                                                        
            IF WS-FSARQSAI       NOT EQUAL  '00'                        
               DISPLAY 'ERRO NO: '                WS-CODIGO-AREA        
               DISPLAY 'STATUS:  '                WS-FSARQSAI           
               MOVE WS-FSARQSAI             TO    WS-AREA-STAT          
               MOVE 'NAO FOI POSSIVEL GRAVAR EM ARQSAI'                 
                                            TO    WS-AREA-MSG           
               CALL WS-ABENDA               USING WS-AREA               
            END-IF. 

	    ADD   1                         TO    WS-QTDE-REGSAI.       
       *----------------------------------------------------------------
        2230-99-GRAVACAO-EXIT.              EXIT.                       
       *----------------------------------------------------------------
       *----------------------------------------------------------------
        2240-CABECALHO-PARTE1               SECTION.                    
       *----------------------------------------------------------------
       * CABECALHO "PADRAO" - MOVIMENTACAO DE DADOS E GRAVACAO          
                                                                        
            ADD   1                          TO    WS-CT-PAG.           
            MOVE  WS-CT-PAG                  TO    PAGINA.              
            MOVE  FUNCTION CURRENT-DATE(7:2) TO    DIA.                 
            MOVE  FUNCTION CURRENT-DATE(5:2) TO    MES.                 
            MOVE  FUNCTION CURRENT-DATE(1:4) TO    ANO.                 
            WRITE REG-ATR                    FROM  CAB-01.              
            WRITE REG-ATR                    FROM  CAB-02.              
            WRITE REG-ATR                    FROM  CAB-03.              
            ADD   3                          TO    WS-CT-LIN.
       *----------------------------------------------------------------
        2240-99-CABECALHO-PARTE1-EXIT.      EXIT.                       
       *----------------------------------------------------------------
       *----------------------------------------------------------------
        2250-CABECALHO-PARTE2               SECTION.                    
       *----------------------------------------------------------------
       * MONTAGEM DO CABECALHO USANDO CAMPOS CPF, NOME E VALOR TOTAL    
                                                                        
            MOVE  SPACES                    TO    REG-ATR.              
            WRITE REG-ATR.                                              
            WRITE REG-ATR                   FROM  CAB-04.               
            ADD   2                         TO    WS-CT-LIN.            
       *----------------------------------------------------------------
        2250-99-CABECALHO-PARTE2-EXIT.      EXIT.                       
       *----------------------------------------------------------------
       *----------------------------------------------------------------
        2255-CARREGA-TABNOVA1               SECTION.                    
       *---------------------------------------------------------------- 
       * CARREGA A TABELA COM OS CAMPOS CPF, DATA E VALOR POR CLIENTE   
                                                                        
            MOVE ZEROS TO       FLAG1.                                  
                                                                        
            PERFORM VARYING     WS-IND                       FROM 1 BY 1
                    UNTIL       WS-IND                       GREATER 40 
                    OR          WS-TAB-CPF(WS-IND)           EQUAL ZEROS
                    OR          FLAG1                        EQUAL 1    
                                                                        
                    IF WS-TAB-CPF(WS-IND)   EQUAL CPF-ENT               
                       MOVE 1               TO    FLAG1                 
                       PERFORM  2256-CARREGA-TABNOVA2                   
                    END-IF                                              
                                                                        
            END-PERFORM.                                                
                                                                        
            IF FLAG1 EQUAL 1                                            
               GO TO 2255-99-CARREGA-TABNOVA1-EXIT 
	    END-IF.                                                     
                                                                        
            IF WS-TAB-CPF(WS-IND)           EQUAL ZEROS                 
               MOVE CPF-ENT                 TO    WS-TAB-CPF(WS-IND)    
               PERFORM  2256-CARREGA-TABNOVA2                           
            END-IF.                                                     
                                                                        
       *----------------------------------------------------------------
        2255-99-CARREGA-TABNOVA1-EXIT.      EXIT.                       
       *----------------------------------------------------------------
       *----------------------------------------------------------------
        2256-CARREGA-TABNOVA2               SECTION.                    
       *----------------------------------------------------------------
            MOVE ZEROS TO FLAG2.                                        
                                                                        
            PERFORM VARYING     WS-IND2                      FROM 1 BY 1
                    UNTIL       WS-IND2                      GREATER 20 
                    OR          WS-TAB-DATA(WS-IND,WS-IND2)  EQUAL ZEROS
		    OR          FLAG2                        EQUAL 1     
                                                                         
                    IF WS-TAB-DATA(WS-IND,WS-IND2) EQUAL DATA-COMPRA-ENT 
                       ADD VALOR-GASTO   TO  WS-TAB-VALOR(WS-IND,WS-IND2)
                       MOVE 1            TO FLAG2                        
                    END-IF                                               
            END-PERFORM.                                                 
                                                                         
            IF FLAG2 EQUAL 1                                             
               GO TO 2256-99-CARREGA-TABNOVA2-EXIT                       
            END-IF.                                                      
                                                                         
            IF WS-TAB-DATA(WS-IND,WS-IND2)  EQUAL ZEROS                  
               MOVE DATA-COMPRA-ENT TO      WS-TAB-DATA(WS-IND,WS-IND2)  
               MOVE VALOR-GASTO     TO      WS-TAB-VALOR(WS-IND,WS-IND2) 
            END-IF.                                                      
                                                                         
       *----------------------------------------------------------------                                                                                           
		      2256-99-CARREGA-TABNOVA2-EXIT.      EXIT.                       
       *----------------------------------------------------------------
       *----------------------------------------------------------------
        2260-IMPRIMA-DET                    SECTION.                    
       *----------------------------------------------------------------
       * IMPRIMA A LINHA DE DETALHE (REGISTRO A SER GRAVADO EM RELAT)   
                                                                        
            MOVE  '3340-GRAVACAO-RELATO'    TO    WS-CODIGO-AREA.       
            MOVE  SPACES                    TO    REG-ATR.              
            MOVE  CPF-ENT(1:9)              TO    CPF-NOVE.             
            MOVE  CPF-ENT(10:2)             TO    CPF-DOIS.             
            MOVE  NOME-ENT                  TO    NOME.                 
            MOVE  VALOR-GASTO               TO    VALOR-TOT-C.          
                                                                        
            WRITE REG-ATR                   FROM  DETALHE.              
                                                                        
       * VERIFICACAO DE FILE STATUS PARA GRAVACAO EM RELATO
	    IF    WS-FSRELATO               NOT   EQUAL '00'            
               DISPLAY 'ERRO NO: '                WS-CODIGO-AREA        
               DISPLAY 'STATUS:  '                WS-FSRELATO           
               MOVE WS-FSRELATO             TO    WS-AREA-STAT          
               MOVE 'NAO FOI POSSIVEL GRAVAR EM RELATO'                 
                                            TO    WS-AREA-MSG           
               CALL WS-ABENDA               USING WS-AREA               
            END-IF.                                                     
                                                                        
            ADD   1                         TO    WS-CT-LIN.            
       *----------------------------------------------------------------
        2260-IMPRIMA-DET-EXIT.              EXIT.                       
       *----------------------------------------------------------------
       *----------------------------------------------------------------
        3000-FINALIZA                       SECTION.                    
       *----------------------------------------------------------------
       * GRAVACAO DE REGISTRO PENDENTE (ULTIMO REGISTRO) 

	    MOVE '3000-FINALIZA'            TO    WS-CODIGO-AREA.
                                                                 
            IF WS-QTDE-REG-LIDOS            GREATER 0            
               PERFORM 2230-GRAVACAO                             
               ADD  WS-TOTAL-GASTO          TO    WS-TOTAL-RELAT 
            END-IF.                                              
                                                                 
            PERFORM 3320-DESCARREGA-TAB1.                        
                                                                 
            IF WS-CT-LIN GREATER 47                              
               MOVE SPACES TO REG-ATR                            
               WRITE REG-ATR                                     
               MOVE ZEROS  TO WS-CT-LIN                          
               PERFORM 2240-CABECALHO-PARTE1                     
               WRITE REG-ATR                                     
               ADD   1     TO WS-CT-LIN                          
            END-IF

	    PERFORM 3350-IMPRIMA-TOT-RELAT.                             
       *----------------------------------------------------------------
       * FECHAMENTO DOS ARQUIVOS E VERIFICACAO DE FILE STATUS           
                                                                        
            CLOSE   ARQENT.                                             
            IF WS-FSARQENT                  NOT   EQUAL '00'            
               DISPLAY 'ERRO NO: '                WS-CODIGO-AREA        
               DISPLAY 'STATUS:  '                WS-FSARQENT           
               MOVE WS-FSARQENT             TO    WS-AREA-STAT          
               MOVE 'ERRO NO FECHAMENTO DO ARQUIVO DE ENTRADA ARQENT'   
                                            TO    WS-AREA-MSG           
               CALL WS-ABENDA               USING WS-AREA               
            END-IF.                                                     
       *----------------------------------------------------------------
            CLOSE   ARQSAI.                                             
            IF WS-FSARQSAI                  NOT   EQUAL '00'            
               DISPLAY 'ERRO NO: '                WS-CODIGO-AREA        
               DISPLAY 'STATUS:  '                WS-FSARQSAI
	       MOVE WS-FSARQSAI             TO    WS-AREA-STAT          
               MOVE 'ERRO NO FECHAMENTO DO ARQUIVO DE SAIDA ARQSAI'     
                                            TO    WS-AREA-MSG           
               CALL WS-ABENDA               USING WS-AREA               
            END-IF.                                                     
       *----------------------------------------------------------------
            CLOSE RELATO.                                               
            IF WS-FSRELATO                  NOT   EQUAL '00'            
               DISPLAY 'ERRO NO: '                WS-CODIGO-AREA        
               DISPLAY 'STATUS:  '                WS-FSRELATO           
               MOVE WS-FSRELATO             TO    WS-AREA-STAT          
               MOVE 'ERRO NO FECHAMENTO DO ARQUIVO RELATORIO'           
                                            TO    WS-AREA-MSG           
               CALL WS-ABENDA               USING WS-AREA               
            END-IF.                                                     
                                                                        
            PERFORM 3360-RESUMO-GERAL.
 		                                                                      
            DISPLAY '                                                  '.
            DISPLAY '**************************************************'.
            DISPLAY '         FIM DO PROGRAMA - THE END                '.
            DISPLAY '**************************************************'.
       *---------------------------------------------------------------- 
        3000-99-FINALIZA-EXIT.              EXIT.                        
       *---------------------------------------------------------------- 
       *---------------------------------------------------------------- 
        3320-DESCARREGA-TAB1                SECTION.                     
       *---------------------------------------------------------------- 
                                                                         
            PERFORM VARYING WS-IND               FROM 1 BY 1             
                    UNTIL   WS-IND               GREATER 40              
                    OR      WS-TAB-CPF(WS-IND)   EQUAL ZEROS             
                                                                         
                    MOVE WS-TAB-CPF(WS-IND)      TO    CPF-11            
                    MOVE CPF-11(1:9)             TO    CPF-N             
		    MOVE CPF-11(10:2)            TO    CPF-D            
                    PERFORM 3340-IMPRIMA-TOT-CLI                        
                    PERFORM 3321-DESCARREGA-TAB2                        
                                                                        
            END-PERFORM.                                                
                                                                        
       *----------------------------------------------------------------
        3320-99-DESCARREGA-TAB1-EXIT.       EXIT.                       
       *----------------------------------------------------------------
       *----------------------------------------------------------------
        3321-DESCARREGA-TAB2                SECTION.                    
       *----------------------------------------------------------------
            PERFORM VARYING WS-IND2                     FROM  1 BY 1    
                    UNTIL   WS-IND2                     GREATER 20      
                    OR      WS-TAB-DATA(WS-IND,WS-IND2) EQUAL ZEROS     
                                                                        
                    IF WS-CT-LIN GREATER 49                             
                       MOVE SPACES TO REG-ATR
		       WRITE REG-ATR                                    
                       MOVE ZEROS  TO WS-CT-LIN                         
                       PERFORM 2240-CABECALHO-PARTE1                    
                       WRITE REG-ATR                                    
                       ADD   1     TO WS-CT-LIN                         
                    END-IF                                              
                                                                        
                    MOVE WS-TAB-DATA(WS-IND,WS-IND2)  TO  DATA-C        
                    MOVE DATA-C(1:2)                  TO     DD         
                    MOVE DATA-C(3:2)                  TO     MM         
                    MOVE DATA-C(5:4)                  TO     AAAA       
                    MOVE WS-TAB-VALOR(WS-IND,WS-IND2) TO  VLR-CLI       
                    PERFORM 3345-IMPRIMA-HIST-DATA                      
                                                                        
            END-PERFORM.                                                
                                                                        
       *----------------------------------------------------------------
        3330-IMPRIMA-ARQ-VAZIO              SECTION.
       *----------------------------------------------------------------
            PERFORM 2240-CABECALHO-PARTE1                               
            WRITE REG-ATR                   FROM DETALHE-VAZIO.         
       *----------------------------------------------------------------
        3330-IMPRIMA-ARQ-VAZIO-EXIT.        EXIT.                       
       *----------------------------------------------------------------
       *----------------------------------------------------------------
        3340-IMPRIMA-TOT-CLI                SECTION.                    
       *----------------------------------------------------------------
       * MOVIMENTACAO DE DADOS PARA O TOTALIZADOR DE CLIENTES           
                                                                        
            IF WS-CT-LIN GREATER 49                                     
               MOVE SPACES TO REG-ATR                                   
               WRITE REG-ATR                                            
               MOVE ZEROS  TO WS-CT-LIN                                 
               PERFORM 2240-CABECALHO-PARTE1                            
               WRITE REG-ATR                                            
               ADD   1     TO WS-CT-LIN 
	    END-IF.                                              
                                                                 
            MOVE  SPACES                     TO    REG-ATR.      
            WRITE REG-ATR.                                       
            MOVE '3340-GRAVACAO-RELATO'     TO    WS-CODIGO-AREA.
                                                                 
            WRITE REG-ATR                   FROM  TOT-03.        
                                                                 
            IF WS-FSRELATO                  NOT   EQUAL  '00'    
               DISPLAY 'ERRO NO: '                WS-CODIGO-AREA 
               DISPLAY 'STATUS:  '                WS-FSRELATO    
               MOVE WS-FSRELATO             TO    WS-AREA-STAT   
               MOVE 'NAO FOI POSSIVEL GRAVAR REGISTRO EM RELATO' 
                                            TO    WS-AREA-MSG    
               CALL WS-ABENDA               USING WS-AREA        
            END-IF.                                              
                                                                 
            ADD   2                         TO    WS-CT-LIN. 
		                                                                      
       *----------------------------------------------------------------
        3340-99-IMPRIMA-TOT-CLI-EXIT.       EXIT.                       
       *----------------------------------------------------------------
       *----------------------------------------------------------------
        3345-IMPRIMA-HIST-DATA              SECTION.                    
       *----------------------------------------------------------------
            WRITE REG-ATR FROM   TOT-04.                                
            ADD   1       TO     WS-CT-LIN.                             
       *----------------------------------------------------------------
        3345-99-IMPRIMA-HIST-DATA-EXIT.     EXIT.                       
       *----------------------------------------------------------------
       *----------------------------------------------------------------
        3350-IMPRIMA-TOT-RELAT              SECTION.                    
       *----------------------------------------------------------------
                                                                        
            IF WS-CT-LIN GREATER 49                                     
               MOVE SPACES TO REG-ATR
	       MOVE ZEROS  TO WS-CT-LIN                                 
               PERFORM 2240-CABECALHO-PARTE1                            
               ADD   1     TO WS-CT-LIN                                 
            END-IF.                                                     
                                                                        
            MOVE  SPACES                    TO   REG-ATR.               
            WRITE REG-ATR.                                              
            MOVE  WS-QTDE-REG-LIDOS         TO   TOT-REG.               
            WRITE REG-ATR                   FROM TOT-01.                
            MOVE  WS-TOTAL-RELAT            TO   VALOR-TOT-F.           
            WRITE REG-ATR                   FROM TOT-02.                
       *----------------------------------------------------------------
        3350-IMPRIMA-TOT-RELAT-EXIT.        EXIT.                       
       *----------------------------------------------------------------
       *----------------------------------------------------------------
        3360-RESUMO-GERAL                   SECTION.                    
       *----------------------------------------------------------------
       * RESULTADO DISPONIVEL NA SAIDA SYSOUT
		                                                                       
            DISPLAY '**************************************************'.
            DISPLAY '       RESUMO/BALANCO GERAL DO PROGRAMA           '.
            DISPLAY '**************************************************'.
            DISPLAY '**************************************************'.
            DISPLAY 'REGISTROS  LIDOS ARQENT  = '      WS-QTDE-REG-LIDOS.
            DISPLAY 'REGISTROS  GRAVADOS EM ARQSAI = ' WS-QTDE-REGSAI   .
            DISPLAY 'REGISTROS  GRAVADOS EM RELATO = ' WS-QTDE-REG-LIDOS.
            DISPLAY 'QUANTIDADE DE QUEBRAS = '         WS-QTDE-REGSAI   .
            DISPLAY 'VALOR TOTAL SUMARIZADO = '        WS-TOTAL-RELAT   .
            DISPLAY '**************************************************'.
            DISPLAY '**************************************************'.
       *---------------------------------------------------------------- 
        3360-RESUMO-GERAL-EXIT.             EXIT.                        
       *---------------------------------------------------------------- 
       *----------------------------------------------------------------                                                                                                                                                                                   
                                                                                                                                  
                                                                                
                                                                                     
                                                                                                                                                                                                                                                                                                    
                                                                                                                                                                                                                                                                              